(

    ..\library\music.stk read-file eval

    (
        [
            [E4 0.5]
            [E4 0.5]
            [C4 0.3]
            [D4 0.3]
            [E4 0.3]
            [D4 0.5]
            [G4 0.7]
            [E4 0.4]
            [D4 0.3]
            [C4 0.3]
            [A3 0.3]
            [B3 0.4]
            [C4 0.4]
            [D4 0.3]
            
        ] (i) (i 0 get i 1 get play-sound) for
    ) true while

) thread

  0     (army) var
100 (economic) var
  0     (city) var
 10   (morale) var

(
    [
        (双子の兄弟、ロムルスとレムスは、神々の子として古代のアルバ・ロンガで誕生した。)
        (彼らは運命に導かれ羊飼いに育てられたが、彼らの運命は、遥かなるローマの創設にあった。)
        (ロムルスとレムスの成長と絆が続く中、兄弟は分かれることを余儀なくされるという悲劇的な運命の転換を迎える。)
        (しかし、ロムルス王の冒険は力と栄光の都市を求め古代の大地にローマの物語が幕を開けるのだった。)
    ] (i) (
        i input
    ) for

    () print ( -- ローマ建国RPG -- ) print
    ((c) 2023 梶塚太智. All rights reserved) print
) (opening) var

(
    () input
    (+-- 現在の状況) print
    (| 市民　　　: 士気レベル) morale concat print
    (| レギオー　: 軍事レベル)   army concat print
    (| ローマ　　: 都市レベル)   city concat print
    (| 経済力　　:) economic concat (デナリウス) concat print
    
    (+-- 元老院) print
    (| A:都市建設する？　B:戦争する？　C:民会で演説する？) print
    
    (インペリウムを入力してください> ) input (select) var
    
    (
        (都市建設にいくら使う？) input develop eval
    ) (
        (
            (戦争にいくら使う？) input ballte eval
        ) (
            (
                (演説にいくら使う？) input speech eval
            ) (
                (元老院 { そんなインペリウムないですよ) print
            ) select (c|C) regex if
        ) select (b|B) regex if
    ) select (a|A) regex if

    clear? eval
) (policy) var

( (cost) var
    cost morale add 2 div (level) var
    city level add (city) var  

    (ローマの都市レベルを) level concat (上げた!) concat print
    economic cost sub (economic) var
    
    city 6 div round (reward) var
    (経済力が) reward concat (デナリウス向上した) concat print
    economic reward add (economic) var

) (develop) var

( (cost) var
    morale cost add (morale) var
    economic cost sub (economic) var

    (市民 { いいぞ！ロムルス王) print
    (市民の士気が) cost concat (アップ!) concat print
) (speech) var

( (cost) var
    (戦争開始！ワーワー) print
    5 14 1 range rand 10 div economic mul round (enemy) var
    
    army cost add (army) var
    economic cost sub (economic) var

    (
        (敵は弱いぞ: 軍事レベルが) enemy concat (しかない) concat print
        (絶対勝てる、勇気を持って行くぞー！) input
        (市民 { おー！) print
        
        (勝利！賠償金) enemy 2 mul (reward) var reward concat (デナリウス獲得) concat print
        economic reward add (economic) var
        (市民の士気レベルが) reward concat (アップ!) concat print
        morale reward add (morale) var    
    ) (
        (元老院 { 勝ち目がないな、和平しとく？ (Yes/No)) print
        (インペリウムを入力してください> ) input (select) var

        (
            (元老院 { えっ！負けるかもしれない戦争をするの？) print

            (敗北・・・。賠償金) enemy 2 mul (damage) var damage concat (デナリウス払う) concat print
            economic damage sub (economic) var
            (市民の士気レベルが) damage concat (ダウン) concat print
            morale damage sub (morale) var    
        ) (
            (元老院は和平の決議をし、ローマは敵と和平を結んだ) print
        ) select (No) equal if
    )
    enemy army morale add 2 div less if
) (ballte) var

(
    (
        (経済が)  economic concat (デナリウスまで悪化した。ゲームオーバー) concat print
        0 exit
    ) (
        (
            () input
            (ローマの都市レベルが) city concat (になった。ゲームクリア！) concat print
            (ユピテルが風を遣わしてロムルス王の伝説の生涯は終わりましたとさ) print
            (めでたしめでたし) print 0 exit
        ) () 9999 city less if
    ) economic -100 less if
) (clear?) var

opening eval (
    policy eval
) (true) while
